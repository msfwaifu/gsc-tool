/* Copyright 2021 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/

%option outfile="./S1/compiler_lexer.cpp"
%option header-file="./S1/compiler_lexer.hpp"
%option prefix = "S1"
%option reentrant
%option case-insensitive
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%top{
#include "S1.hpp"
#include "compiler_parser.hpp"
#ifndef YY_NO_UNISTD_H
#define YY_NO_UNISTD_H
#endif
}

%{
#define YY_USER_ACTION  loc.columns(yyleng);
%}

RGX_FILE			([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_NAME			[_A-Za-z][_A-Za-z0-9]*
RGX_STRING			\"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT			[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER			[0-9]+
RGX_DEFAULT			(.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

%{
    loc.step();
%}

[ \t\r]                     { loc.step(); }

\n							{ loc.lines(yyleng); loc.step(); }

"//".*

"/*"						{ BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>.
<COMMENT_BLOCK_STATE>\n		{ loc.lines(yyleng); loc.step(); }
<COMMENT_BLOCK_STATE>"*/"	{ BEGIN(INITIAL); }

"/#"						{ BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>.
<DEVELOPER_BLOCK_STATE>\n	{ loc.lines(yyleng); loc.step(); }
<DEVELOPER_BLOCK_STATE>"#/"	{ BEGIN(INITIAL); }	

"#include"			{ return S1::parser::make_INCLUDE(loc); }
"#using_animtree"	{ return S1::parser::make_USINGTREE(loc); }
"#animtree"			{ return S1::parser::make_ANIMTREE(loc); }
"endon"				{ return S1::parser::make_ENDON(loc); }
"notify"			{ return S1::parser::make_NOTIFY(loc); }
"wait"				{ return S1::parser::make_WAIT(loc); }
"waittill"			{ return S1::parser::make_WAITTILL(loc); }
"waittillmatch"		{ return S1::parser::make_WAITTILLMATCH(loc); }
"waittillframeend"	{ return S1::parser::make_WAITTILLFRAMEEND(loc); }
"if"				{ return S1::parser::make_IF(loc); }
"else"				{ return S1::parser::make_ELSE(loc); }
"while"				{ return S1::parser::make_WHILE(loc); }
"for"				{ return S1::parser::make_FOR(loc); }
"foreach"			{ return S1::parser::make_FOREACH(loc); }
"in"				{ return S1::parser::make_IN(loc); }
"switch"			{ return S1::parser::make_SWITCH(loc); }
"case"				{ return S1::parser::make_CASE(loc); }
"default"			{ return S1::parser::make_DEFAULT(loc); }
"break"				{ return S1::parser::make_BREAK(loc); }
"continue"			{ return S1::parser::make_CONTINUE(loc); }
"return"			{ return S1::parser::make_RETURN(loc); }
"thread"			{ return S1::parser::make_THREAD(loc); }
"childthread"		{ return S1::parser::make_CHILDTHREAD(loc); }
"thisthread"		{ return S1::parser::make_THISTHREAD(loc); }
"call"				{ return S1::parser::make_CALL(loc); }
"true"				{ return S1::parser::make_TRUE(loc); }
"false"				{ return S1::parser::make_FALSE(loc); }
"undefined"			{ return S1::parser::make_UNDEFINED(loc); }
"size"				{ return S1::parser::make_SIZE(loc); }
"game"				{ return S1::parser::make_GAME(loc); }
"self"				{ return S1::parser::make_SELF(loc); }
"anim"				{ return S1::parser::make_ANIM(loc); }
"level"				{ return S1::parser::make_LEVEL(loc); }
"[]"				{ return S1::parser::make_EMPTY_ARRAY(loc); }
\(					{ return S1::parser::make_LPAREN(loc); }
\)					{ return S1::parser::make_RPAREN(loc); }
\{					{ return S1::parser::make_LBRACE(loc); }
\}					{ return S1::parser::make_RBRACE(loc); }
\[					{ return S1::parser::make_LBRACKET(loc); }
\]					{ return S1::parser::make_RBRACKET(loc); }
\,					{ return S1::parser::make_COMMA(loc); }
\.					{ return S1::parser::make_DOT(loc); }
\:\:				{ return S1::parser::make_DOUBLECOLON(loc); }
\:					{ return S1::parser::make_COLON(loc); }
\;					{ return S1::parser::make_SEMICOLON(loc); }
\+\+				{ return S1::parser::make_INCREMENT(loc); }
\-\-				{ return S1::parser::make_DECREMENT(loc); }
\<\<\=				{ return S1::parser::make_ASSIGN_LSHIFT(loc); }
\>\>\=				{ return S1::parser::make_ASSIGN_RSHIFT(loc); }
\<\<				{ return S1::parser::make_LSHIFT(loc); }
\>\>				{ return S1::parser::make_RSHIFT(loc); }
\|\|				{ return S1::parser::make_OR(loc); }
\&\&				{ return S1::parser::make_AND(loc); }
\=\=				{ return S1::parser::make_EQUALITY(loc); }
\!\=				{ return S1::parser::make_INEQUALITY(loc); }
\<\=				{ return S1::parser::make_LESS_EQUAL(loc); }
\>\=				{ return S1::parser::make_GREATER_EQUAL(loc); }
\<					{ return S1::parser::make_LESS(loc); }
\>					{ return S1::parser::make_GREATER(loc); }
\+\=				{ return S1::parser::make_ASSIGN_ADD(loc); }
\-\=				{ return S1::parser::make_ASSIGN_SUB(loc); }
\*\=				{ return S1::parser::make_ASSIGN_MULT(loc); }
\/\=				{ return S1::parser::make_ASSIGN_DIV(loc); }
\%\=				{ return S1::parser::make_ASSIGN_MOD(loc); }
\|\=				{ return S1::parser::make_ASSIGN_BITWISE_OR(loc); }
\&\=				{ return S1::parser::make_ASSIGN_BITWISE_AND(loc); }
\^\=				{ return S1::parser::make_ASSIGN_BITWISE_EXOR(loc); }
\=					{ return S1::parser::make_ASSIGN(loc); }
\+					{ return S1::parser::make_ADD(loc); }
\-					{ return S1::parser::make_SUB(loc); }
\*					{ return S1::parser::make_MULT(loc); }
\/					{ return S1::parser::make_DIV(loc); }
\%					{ return S1::parser::make_MOD(loc); }
\!					{ return S1::parser::make_NOT(loc); }
\~					{ return S1::parser::make_COMPLEMENT(loc); }
\|					{ return S1::parser::make_BITWISE_OR(loc); }
\&					{ return S1::parser::make_BITWISE_AND(loc); }
\^					{ return S1::parser::make_BITWISE_EXOR(loc); }

{RGX_FILE}			{ return S1::parser::make_FILE(std::string(yytext), loc); }
{RGX_NAME}			{ return S1::parser::make_NAME(utils::string::to_lower(yytext), loc); }
\&{RGX_STRING}		{ return S1::parser::make_ISTRING(std::string(yytext).substr(1), loc); }
{RGX_STRING}		{ return S1::parser::make_STRING(std::string(yytext), loc); }
{RGX_FLOAT}			{ return S1::parser::make_FLOAT(std::string(yytext), loc); }
{RGX_INTEGER}		{ return S1::parser::make_INTEGER(std::string(yytext), loc); }

<<EOF>>				{ return S1::parser::make_S1EOF(loc); }
<*>{RGX_DEFAULT}	{ throw S1::parser::syntax_error(loc, "invalid character: \'" + std::string(yytext) + "\'"); }
%%
